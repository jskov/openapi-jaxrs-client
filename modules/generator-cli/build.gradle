plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '8.1.1'

    id 'dk.mada.java'
    id 'dk.mada.jacoco'
    id 'dk.mada.eclipse'
    id 'dk.mada.pom-jaxrs'
}

ext {
    builtOn = LocalDate.now()
}

dependencies {
    implementation project(':generator-api')
    implementation project(':generator')
    implementation 'info.picocli:picocli:4.7.5'
    implementation 'org.slf4j:slf4j-jdk14'

    testRuntimeOnly project(':generator')
}

shadowJar {
   archiveBaseName.set('ojc')
   archiveClassifier.set('cli')
   archiveVersion.set(project.version)

   destinationDirectory = project.rootProject.file("build/dist")
}

application {
    mainClass = "dk.mada.jaxrs.generator.cli.Main"
}

processResources {
    inputs.property("version", project.version)
    inputs.property("builtOn", builtOn)

    filesMatching("**/openapi-jaxrs-client-version.properties") {
        filter(s -> s
            .replaceAll("version.*", "version = ${project.version}")
            .replaceAll("builtOn.*", "builtOn = ${builtOn}")
        )
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'OpenAPI JAXRS generator command'
                description = 'This provides a way for starting the generator from command line'
            }
        }
    }
}
