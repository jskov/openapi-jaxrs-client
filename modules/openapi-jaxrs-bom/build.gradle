plugins {
    id('java-platform')
    id 'maven-publish'

    id 'dk.mada.eclipse'
}

group = 'dk.mada.jaxrs'

// allow the definition of dependencies to other platforms
javaPlatform.allowDependencies()

// Some of these are compile-only and should be moved to a compile platform
dependencies {
    constraints {
        api project(':generator-api')

        api 'info.picocli:picocli:4.7.5'

        api 'io.swagger.parser.v3:swagger-parser:2.1.16'
        // swagger.parser comes with vulnerable rhino + commons-codec 
        api 'org.mozilla:rhino:1.7.14'
        api 'commons-codec:commons-codec:1.16.0'

        api 'org.slf4j:slf4j-jdk14:2.0.9'
    }
}

publishing {
    repositories {
        maven {
            name = "dist"
            url "file://${distDir}"
        }
    }
    publications {
        myPlatform(MavenPublication) {
            from components.javaPlatform
            pom {
                name = 'OpenAPI JAXRS client'
                description = 'A JAXRS client generator'
                url = 'https://github.com/jskov/openapi-jaxrs-client'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'jskov'
                        name = 'Jesper Skov'
                        email = 'jskov@mada.dk'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/jskov/openapi-jaxrs-client.git'
                    developerConnection = 'scm:git:ssh://github.com:jskov/openapi-jaxrs-client.git'
                    url = 'https://github.com/jskov/openapi-jaxrs-client/'
                }
            }
        }
    }
}
