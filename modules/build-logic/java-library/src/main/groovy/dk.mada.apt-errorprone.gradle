plugins {
    id('java')
    id('dk.mada.java')

    id 'net.ltgt.errorprone'
}

dependencies {
    errorprone          (platform('dk.mada.jaxrs:openapi-jaxrs-bom'))

    implementation      'org.jspecify:jspecify'
    annotationProcessor 'com.uber.nullaway:nullaway'
    errorprone          'com.google.errorprone:error_prone_core'
}

import net.ltgt.gradle.errorprone.CheckSeverity

tasks.withType(JavaCompile).configureEach {
    options.errorprone {
        disableWarningsInGeneratedCode = false

        // ImmutableEnumChecker:
        //  https://github.com/google/error-prone/issues/1542 (Set.of)
        //  ? records
        // InvalidParam:
        //  https://github.com/google/error-prone/issues/2321
        // JavaTimeDefaultTimeZone:
        //  Timezone is not relevant
        disable("ImmutableEnumChecker", "InvalidParam", "JavaTimeDefaultTimeZone")
        excludedPaths = ".*/(azure/.*|petstore/dto/Error.java|manual/.*|opts/generator/zoneddatetime/dto/LocalTime.java|OpenapiGeneratorUtils.java|generated/.*)"

        if (!name.toLowerCase().contains("test")) {
            check("NullAway", CheckSeverity.ERROR)
            option("NullAway:AnnotatedPackages", "dk.mada")
        }
    }
}
