// Configures errorprone plugin with nullaway

//  Project is allowed to configure some properties via the
//  file src/config/errorprone/dk.mada.apt-errorprone.properties

plugins {
    id('java')
    id('dk.mada.java')

    id 'net.ltgt.errorprone'
}

ext {
    cachedProps = null
}

dependencies {
    errorprone          (platform('dk.mada.jaxrs:apt-platform'))

    implementation      'org.jspecify:jspecify'
    annotationProcessor 'com.uber.nullaway:nullaway'
    errorprone          'com.google.errorprone:error_prone_core'
}

import net.ltgt.gradle.errorprone.CheckSeverity

Properties readProps() {
    if (cachedProps == null) {
        cachedProps = new Properties();
        def f = project.file("src/config/errorprone/dk.mada.apt-errorprone.properties")
        if (!f.exists()) {
            f = project.file("src/config/errorprone/dk.mada.apt-errorprone.properties")
        }
        if (f.exists()) {
            try (InputStream is = new FileInputStream(f)) {
                cachedProps.load(is)
            }
        }
    }
    return cachedProps
}

tasks.withType(JavaCompile).configureEach {
    options.errorprone {
        disableWarningsInGeneratedCode = false

        readProps().getProperty("disable", "").split(",").each {
            String checker = it.trim()
            if (!checker.isEmpty()) {
                disable(checker)
            }
        }
        excludedPaths = readProps().getProperty("excludedPaths")

        if (!name.toLowerCase().contains("test")) {
            check("NullAway", CheckSeverity.ERROR)
            option("NullAway:AnnotatedPackages", "dk.mada")
        }
    }
}
