plugins {
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '2.0.0'
}

apply from: "${project.rootDir}/gradle/dk.mada.java.gradle"

dependencies {
    implementation      project(':generator-api')

    testImplementation  (libs.bundles.testUnit)
}

gradlePlugin {
    website = 'https://github.com/jskov/openapi-jaxrs-client'
    vcsUrl = 'https://github.com/jskov/openapi-jaxrs-client'
    plugins {
        openApiJaxrs {
            id = 'dk.mada.jaxrs'
            displayName = 'Plugin for generating JAX-RS code from OpenAPI documents'
            description = 'A plugin that generates JAX-RS code from OpenApi documents. It has many configuration options and a broad test suite to cover many corner cases.'
            tags.set(['openapi', 'swagger', 'codegen', 'generator', 'jax-rs', 'microprofile'])
            implementationClass = 'dk.mada.jaxrs.gradle.JaxrsPlugin'
        }
    }
}

test {
    testLogging.showStandardStreams = true

    systemProperty "releaseVersion", project.version

    dependsOn(
        ":generator:publishToMavenLocal",
        ":generator-api:publishToMavenLocal",
        ":model:publishToMavenLocal",
        ":openapi-jaxrs-bom:publishToMavenLocal",
        ":parser:publishToMavenLocal",
    )

//    forkEvery = 1
}
